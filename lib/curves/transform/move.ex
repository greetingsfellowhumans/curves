defmodule Curves.Transform.Move do
  @moduledoc ~s"""
  Move the curve around on the x and y planes, without otherwise altering the curve.
  """
  import Curves.Utils.Point, only: [new_point: 1]
  alias Curves.Utils.Types, as: T
  #@behaviour Curves.Transform.Transformer

  @doc ~s"""
  Ignore current position and set a new one.

  ## Examples
      iex> curve = Curves.define_bezier(:linear)
      iex> {x, _y} = Curves.solve!(curve, 0.5)
      iex> x
      0.5
      iex> curve = Move.set_position(curve, {10, 10})
      iex> {x, _y} = Curves.solve!(curve, 0.5)
      iex> x
      10.5
  """
  @spec set_position(curve :: T.curve_struct(), T.point_tuple()) :: T.curve_struct()
  def set_position(curve, origin) do
    transform(curve, fn _ -> new_point(origin) end)
  end


  @doc ~s"""
  Using the current position increase both the x and y coordinates.
  It is also possible to decrease by passing a negative number

  ## Examples
      iex> curve = Curves.define_bezier(:linear, origin: {10, 10})
      iex> {x, _y} = Curves.solve!(curve, 0.5)
      iex> x
      10.5
      iex> curve = Move.inc_position(curve, {10, 10})
      iex> {x, _y} = Curves.solve!(curve, 0.5)
      iex> x
      20.5
  """
  @spec inc_position(curve :: T.curve_struct(), T.point_tuple()) :: T.curve_struct()
  def inc_position(curve, pos) do
    p = new_point(pos)
    transform(curve, fn origin ->  Nx.add(origin, p) end)
  end

  @doc ~s"""
  Using the current position increase the x coordinates.
  It is also possible to decrease by passing a negative number

  ## Examples
      iex> curve = Curves.define_bezier(:linear, origin: {10, 10})
      iex> {x, _y} = Curves.solve!(curve, 0.5)
      iex> x
      10.5
      iex> curve = Move.inc_x(curve, 10)
      iex> {x, y} = Curves.solve!(curve, 0.5)
      iex> x
      20.5
      iex> y
      10.5
  """
  def inc_x(curve, xinc) do
    p = new_point({xinc, 0.0})
    transform(curve, fn origin ->  Nx.add(origin, p) end)
  end


  @doc ~s"""
  Using the current position increase the y coordinates.
  It is also possible to decrease by passing a negative number

  ## Examples
      iex> curve = Curves.define_bezier(:linear, origin: {10, 10})
      iex> {x, y} = Curves.solve!(curve, 0.5)
      iex> x
      10.5
      iex> y
      10.5
      iex> curve = Move.inc_y(curve, -10)
      iex> {x, y} = Curves.solve!(curve, 0.5)
      iex> x
      10.5
      iex> y
      0.5
  """
  def inc_y(curve, yinc) do
    p = new_point({0.0, yinc})
    transform(curve, fn origin ->  Nx.add(origin, p) end)
  end

  #@impl true
  defp transform(curve, cb) do
    Map.update!(curve, :origin, cb)
  end

end

